plugins {
    id 'org.springframework.boot' version '2.1.3.RELEASE'
}

apply plugin: 'java'
apply plugin: "io.spring.dependency-management"

configurations {
    zipped
}

jar {
    baseName = 'peppol-inbound'
    version = ''
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://repo.spring.io/milestone' }
}

dependencies {
    zipped files('libs/oxalis-as4.zip')
    compile files({ tasks.installDependencies.extractedJars })

    compile 'com.google.inject.extensions:guice-servlet:4.2.2'
    compile 'com.google.guava:guava:24.0-jre'
    compile 'com.opuscapita.peppol:peppol-commons:1.0.15'

    compile group: 'no.difi.oxalis', name: 'oxalis-as2', version: '4.0.2'
    compile group: 'no.difi.oxalis', name: 'oxalis-api', version: '4.0.2'
    compile group: 'no.difi.oxalis', name: 'oxalis-statistics', version: '4.0.2'

    testCompile 'junit:junit:4.12'
    testCompile 'org.springframework.boot:spring-boot-starter-test:2.1.0.RELEASE'
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Greenwich.RELEASE'
    }
}

task installDependencies(type: Sync) {
    def extractDir = "${buildDir}/libs"

    ext.extractedJars = fileTree(extractDir) {
        include "**/*.jar"
        builtBy "installDependencies"
    }

    dependsOn configurations.zipped
    from {
        configurations.zipped.collect { zipTree(it) }
    }
    into extractDir
}